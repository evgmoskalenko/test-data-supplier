plugins {
    id 'java-library'
    id 'org.gradle.java.experimental-jigsaw' version '0.1.1'
}

group 'io.github.sskorol'
version = version
description 'Test Data Supplier is an extended version of common TestNG DataProvider.'

ext {
    gradleScriptDir = "${rootProject.projectDir}/gradle"
    projectUrl = "https://github.com/sskorol/test-data-supplier"
}

javaModule.name = 'io.github.sskorol'

apply plugin: 'java'

sourceCompatibility = JavaVersion.VERSION_1_9
targetCompatibility = JavaVersion.VERSION_1_9

repositories {
    jcenter()
}

configurations {
    agent
}

dependencies {
    agent 'org.aspectj:aspectjweaver:1.8.10'
    compile('org.testng:testng:6.11',
            'org.jooq:joor:0.9.6',
            'one.util:streamex:0.6.5',
            'io.vavr:vavr:0.9.0',
            'org.aspectj:aspectjrt:1.8.10')
    testCompile('org.assertj:assertj-core:3.8.0',
            'junit:junit:4.12')
}

task wrapper(type: Wrapper) {
    gradleVersion '4.1'
}

test {
    doFirst {
        jvmArgs "-javaagent:${configurations.agent.singleFile}"
    }

    useTestNG() {
        suites 'src/test/resources/smoke-suite.xml'
    }
}